module com.farthergate.goldberg.Config

import "package://pkg.pkl-lang.org/pkl-go/pkl.golang@0.12.1#/go.pkl"

class Source {
  repo: String = ""
}

class CI {
  image: String = "fedora/43"
  packages: Listing<String> = new Listing { "wget"; "tar"; "gzip" }
  oauth: Listing<String> = new {}
  environment: Mapping<String, String> = new {}
}

class Preset {
  name: String
  defaultSteps: Listing<Step>
}

class Step {
  name: String
  commands: Listing<Listing<String>>
  packages: Listing<String> = new {}
  resources: Mapping<String, String> = new {}
}

function getDefaultSteps(presets: Listing<Preset>) = new Listing {
  for (preset in presets) {
    ...preset.defaultSteps
  }
}

function command(cmd: Listing<String>) = new Step {
  name = cmd[0]
  commands {
    cmd
  }
}

function single(step: Step) = new Preset {
  name = "custom"
  defaultSteps {
    step
  }
}

fixed versionedName: String = name + "-" + version

name: String = ""
version: String = ""
use: Listing<Preset> = new Listing {}
source: Source = new Source {}
fixed steps: Listing<Step> = getDefaultSteps(use)
tasks: Mapping<String, Listing<Preset>> = new Mapping {}
fixed taskSteps: Map<String, Listing<Step>> = tasks.toMap().mapValues((_, v) -> getDefaultSteps(v))
ci: CI = new CI {}
