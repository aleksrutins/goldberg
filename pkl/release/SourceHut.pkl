module com.farthergate.goldberg.release.SourceHut

import "../Config.pkl"

class Release {
  source: Config.Source
  assets: Listing<String>
  rev: String | Null

  local function releaseCmds() = new Listing {
    for (asset in assets) {
      new Listing {
        "hut"
        "git"
        "artifact"
        "upload"
        asset
        when (rev != null) { "--rev"; rev }
      }
    }
  }

  fixed step = new Config.Step {
    name = "release"
    packages {
      "hut"
    }
    commands = releaseCmds()
  }
}

class DeployPages {
  publicDir: String
  tarFile: String = "../site.tar.gz"
  domain: String

  fixed compressStep = new Config.Step {
    name = "compress"
    packages { "tar"; "gzip" }
    commands {
      new { "tar"; "-C"; publicDir; "--hard-dereference"; "-cvhz"; "."; "-f"; tarFile }
    }
  }

  fixed deployStep = new Config.Step {
    name = "deploy"
    packages {
      "hut"
    }
    commands {
      new { "hut"; "pages"; "publish"; "-d"; domain; tarFile }
    }
  }
}

function release(source_: Config.Source, assets_: Listing<String>, rev_: String | Null) = new Config
  .Preset {
  name = "sourcehut-release"
  defaultSteps {
    new Release { source = source_; assets = assets_; rev = rev_ }.step
  }
}

function deployPages(publicDir_: String, domain_: String) = new Config.Preset {
  local pages = new DeployPages { publicDir = publicDir_; domain = domain_ }
  name = "sourcehut-pages"
  defaultSteps {
    pages.compressStep
    pages.deployStep
  }
}
